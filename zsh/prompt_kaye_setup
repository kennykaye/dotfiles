#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/nBEEZ.png
#

# Load dependencies.
pmodload 'helper'

function prompt_sorin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sorin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sorin_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_sorin_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sorin_pwd
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby info
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Get Virtual env info
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Get Node info
  if (( $+functions[node-info] )); then
    node-info
  fi
}

function prompt_sorin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling infos before each command.
  add-zsh-hook precmd prompt_sorin_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}‚ô∫%f'
  zstyle ':prezto:module:editor:info:keymap:primary' format '‚îó %B%F{red}‚ùØ%F{yellow}‚ùØ%F{green}‚ùØ%f%b'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '‚îó %B%F{green}‚ùÆ%F{yellow}‚ùÆ%F{red}‚ùÆ%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}‚úö%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}‚¨Ü%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}‚¨á%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%%B%F{red}[%f%%b%%B%F{green}%b%f%%b'
  zstyle ':prezto:module:git:info:clean' format "%B%F{red}]%f%b"
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}‚úñ%f%%b'
  zstyle ':prezto:module:git:info:dirty' format "%%B%F{red}]%f%%b %F{yellow}‚ö°%f "
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}‚ú±%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}‚ûú%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}‚ú≠%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}‚ïê%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}ùñÆ%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '$(coalesce "%b" "%p" "%c")%C%D%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{red}rb:%v%f'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '%F{220}py:%v%f'

  # Set node-info parameters
  zstyle ':prezto:module:node:info:version' format '%F{green}node:%v%f'

  # Œª
  # Define prompts.
  PROMPT='
‚îè %F{yellow}%n%f at %F{red}${_prompt_sorin_pwd}%f on ${git_info:+${(e)git_info[prompt]}} ${ruby_info[version]} ${node_info[version]} ${python_info[virtualenv]}
%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}‚èé%f)${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_sorin_setup "$@"
